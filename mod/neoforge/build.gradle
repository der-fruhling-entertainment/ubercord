//file:noinspection DependencyNotationArgument
import org.gradle.api.internal.component.ArtifactType

plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    implementation(platform("net.derfruhling.discord:socialsdk4j-bom:${project.socialsdk4j_version}")) { changing = true }
    shadowBundle(platform("net.derfruhling.discord:socialsdk4j-bom:${project.socialsdk4j_version}")) { changing = true }

    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"
    include(modImplementation "dev.lambdaurora:spruceui:$rootProject.spruceui_version")

    modApi("me.shedaniel.cloth:cloth-config-neoforge:15.0.140")

    implementation 'com.auth0:java-jwt:4.5.0'
    implementation 'com.auth0:jwks-rsa:0.22.1'

    api('net.derfruhling.discord:socialsdk4j') { changing = true }
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'linux', changing: true
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'windows', changing: true
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'macos', changing: true
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'linux'
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'windows'
    runtimeOnly group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'macos'

    common(project(path: ':mod:common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':mod:common', configuration: 'transformProductionNeoForge')
    shadowBundle('net.derfruhling.discord:socialsdk4j') { changing = true }
    shadowBundle 'com.auth0:java-jwt:4.5.0'
    shadowBundle 'com.auth0:jwks-rsa:0.22.1'

    // TODO download automatically?
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'linux', changing: true
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'windows', changing: true
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk4j', classifier: 'macos', changing: true
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'linux'
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'windows'
    shadowBundle group: 'net.derfruhling.discord', name: 'socialsdk', classifier: 'macos'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
