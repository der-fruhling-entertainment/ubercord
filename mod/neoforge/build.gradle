import org.gradle.api.internal.component.ArtifactType

plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"
    include(modImplementation "dev.lambdaurora:spruceui:$rootProject.spruceui_version")

    modApi("me.shedaniel.cloth:cloth-config-neoforge:15.0.140")

    implementation 'com.auth0:java-jwt:4.5.0'
    implementation 'com.auth0:jwks-rsa:0.22.1'

    api 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT'
    runtimeOnly 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:linux'
    runtimeOnly 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:windows'
    runtimeOnly 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:macos'
    runtimeOnly 'net.derfruhling.discord:socialsdk:1.2.8730:linux'
    runtimeOnly 'net.derfruhling.discord:socialsdk:1.2.8730:windows'
    runtimeOnly 'net.derfruhling.discord:socialsdk:1.2.8730:macos'

    common(project(path: ':mod:common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':mod:common', configuration: 'transformProductionNeoForge')
    shadowBundle 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT'
    shadowBundle 'com.auth0:java-jwt:4.5.0'
    shadowBundle 'com.auth0:jwks-rsa:0.22.1'

    // TODO download automatically?
    shadowBundle 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:linux'
    shadowBundle 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:windows'
    shadowBundle 'net.derfruhling.discord:socialsdk4j:1.0-SNAPSHOT:macos'
    shadowBundle 'net.derfruhling.discord:socialsdk:1.2.8730:linux'
    shadowBundle 'net.derfruhling.discord:socialsdk:1.2.8730:windows'
    shadowBundle 'net.derfruhling.discord:socialsdk:1.2.8730:macos'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
